apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'dagger.hilt.android.plugin'
apply plugin: "androidx.navigation.safeargs.kotlin"

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.2"

    defaultConfig {
        applicationId "com.martonvago.archelon"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "com.martonvago.archelon.CustomTestRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    dataBinding {
        enabled = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }
}

dependencies {
    def fragment_version = '1.2.5'
    def espresso_version = '3.3.0'
    def truth_version = '1.1'
    def room_version = '2.2.5'
    def androidx_hilt_version = '1.0.0-alpha02'
    def threetenabp_version = '1.3.0'
    def material_version = '1.2.1'
    def mockk_version = '1.10.3-jdk8'
    def coroutines_test_version = '1.3.2'
    def core_testing_version = '2.1.0'

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation "com.google.android.material:material:$material_version"

    androidTestImplementation "androidx.test.espresso:espresso-core:$espresso_version"
    // Exclude conflicting transitive dependency
    androidTestImplementation("com.google.truth:truth:$truth_version") {
        exclude group: "com.google.code.findbugs", module: "jsr305"
    }
    // Exclude conflicting transitive dependency
    debugImplementation("androidx.fragment:fragment-testing:$fragment_version") {
        exclude group: 'androidx.test', module: 'core'
    }

    androidTestImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutines_test_version"
    androidTestImplementation "androidx.arch.core:core-testing:$core_testing_version"

    // Room
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    // ThreeTenABP added to be able to use java.time without bumping the minSdkVersion
    // or the gradle plugin version (gradle plugin 4.0.0+ needed for desugaring).
    implementation "com.jakewharton.threetenabp:threetenabp:$threetenabp_version"
    // https://github.com/JakeWharton/ThreeTenABP/issues/14
    androidTestImplementation("org.threeten:threetenbp:1.5.0") {
        exclude group: 'com.jakewharton.threetenabp', module:'threetenabp'
    }

    // JUnit 4
    testImplementation 'junit:junit:4.13.1'
    androidTestImplementation "androidx.test.ext:junit:1.1.2"

    // MockK
    androidTestImplementation "io.mockk:mockk-android:$mockk_version"
    testImplementation "io.mockk:mockk:$mockk_version"

    // Navigation dependencies
    implementation "androidx.navigation:navigation-fragment-ktx:$navigation_version"
    implementation "androidx.navigation:navigation-ui-ktx:$navigation_version"
    androidTestImplementation "androidx.navigation:navigation-testing:$navigation_version"

    // Hilt dependencies
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-android-compiler:$hilt_version"
    implementation "androidx.hilt:hilt-lifecycle-viewmodel:$androidx_hilt_version"
    kapt "androidx.hilt:hilt-compiler:$androidx_hilt_version"

    androidTestImplementation "com.google.dagger:hilt-android-testing:$hilt_version"
    kaptAndroidTest "com.google.dagger:hilt-android-compiler:$hilt_version"
}
